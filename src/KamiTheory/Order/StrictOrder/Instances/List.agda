-- SPDX-FileCopyrightText: 2024 Maxim Urschumzew <mxmurw@determi.io>
--
-- SPDX-License-Identifier: MIT

{-# OPTIONS --allow-unsolved-metas #-}

module KamiTheory.Order.StrictOrder.Instances.List where

open import Agora.Conventions hiding (Î£ ; Lift ; k)

open import KamiTheory.Basics
open import KamiTheory.Order.StrictOrder.Base


-- We show that there is a strict (lexicographic) order on List A for a strict order A

module _ {A : ğ’° ğ‘–} {{Ap : hasStrictOrder A}} where
  data _<-List_ : (u v : List A) -> ğ’° ğ‘– where
    empty : âˆ€{u us} -> [] <-List (u âˆ· us)
    take : âˆ€{u us v vs} -> (u < v) -> (u âˆ· us) <-List (v âˆ· vs)
    next : âˆ€{u vs ws} -> vs <-List ws -> (u âˆ· vs) <-List (u âˆ· ws)

  irrefl-<-List : âˆ€{us} -> Â¬ us <-List us
  irrefl-<-List (take x) = irrefl-< x
  irrefl-<-List (next p) = irrefl-<-List p

  trans-<-List : âˆ€{us vs ws} -> us <-List vs -> vs <-List ws -> us <-List ws
  trans-<-List empty (take x) = empty
  trans-<-List empty (next q) = empty
  trans-<-List (take x) (take y) = take (trans-< x y)
  trans-<-List (take x) (next q) = take x
  trans-<-List (next p) (take x) = take x
  trans-<-List (next p) (next q) = next (trans-<-List p q)

  head-<-List : âˆ€{a b} {as bs} -> (a âˆ· as) <-List (b âˆ· bs) -> Â¬ (b < a)
  head-<-List (take p) = asym-< p
  head-<-List (next p) = irrefl-<

  tail-<-List : âˆ€{a} {as bs} -> (a âˆ· as) <-List (a âˆ· bs) -> (as <-List bs)
  tail-<-List (take x) = âŠ¥-elim (irrefl-< x)
  tail-<-List (next p) = p

  conn-<-List : (a b : List A) -> Tri (a <-List b) (a â‰¡ b) (b <-List a)
  conn-<-List [] [] = triâ‰¡ (Î» ()) refl-â‰¡ (Î» ())
  conn-<-List [] (x âˆ· b) = tri< empty (Î» ()) (Î» ())
  conn-<-List (x âˆ· a) [] = tri> (Î» ()) (Î» ()) empty
  conn-<-List (a âˆ· as) (b âˆ· bs) with conn-< a b
  ... | tri< a<b aâ‰¢b aâ‰¯b = tri< (take a<b) (Î» p -> aâ‰¢b (head-â‰¡ p)) (Î» p -> head-<-List p a<b)
  ... | tri> aâ‰®b aâ‰¢b a>b = tri> ((Î» p -> head-<-List p a>b)) ((Î» p -> aâ‰¢b (head-â‰¡ p))) (take a>b)
  ... | triâ‰¡ aâ‰®b refl-â‰¡ aâ‰¯b with conn-<-List as bs
  ... | tri< as<bs asâ‰¢bs asâ‰¯bs = tri< (next as<bs) (Î» p -> asâ‰¢bs (tail-â‰¡ p)) (Î» p â†’ asâ‰¯bs (tail-<-List p))
  ... | triâ‰¡ aâ‰®bâ‚ refl-â‰¡ aâ‰¯bâ‚ = triâ‰¡ irrefl-<-List refl-â‰¡ irrefl-<-List
  ... | tri> asâ‰®bs asâ‰¢bs as>bs = tri> ((Î» p â†’ asâ‰®bs (tail-<-List p))) ((Î» p -> asâ‰¢bs (tail-â‰¡ p))) (next as>bs)

  force-â‰¡-<-List : âˆ€{as bs} -> (a b : as <-List bs) â†’ a â‰¡ b
  force-â‰¡-<-List empty empty = refl-â‰¡
  force-â‰¡-<-List (take x) (take y) = cong-â‰¡ take (force-â‰¡ x y)
  force-â‰¡-<-List (take x) (next b) = âŠ¥-elim (irrefl-< x)
  force-â‰¡-<-List (next a) (take x) = âŠ¥-elim (irrefl-< x)
  force-â‰¡-<-List (next a) (next b) = cong-â‰¡ next (force-â‰¡-<-List a b)

  instance
    isProp:<-List : âˆ€{as bs : List A} -> isProp (as <-List bs)
    isProp:<-List = record { force-â‰¡ = force-â‰¡-<-List }

  instance
    isStrictOrder:<-List : isStrictOrder _<-List_
    isStrictOrder:<-List = record
      { irrefl-< = irrefl-<-List
      ; trans-< = trans-<-List
      ; conn-< = conn-<-List
      }

  instance
    hasStrictOrder:List : hasStrictOrder (List A)
    hasStrictOrder:List = record { _<_ = _<-List_ }



